buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = "0.1"
group = "com.replaymod.pixelcam"
archivesBaseName = "pixelcam"

minecraft {
    coreMod = "com.replaymod.pixelcam.coremod.LoadingPlugin"
    version = "1.9-12.16.0.1865-1.9"
    runDir = "run"
    mappings = "snapshot_20160312"
}

dependencies {
    compile 'joda-time:joda-time:2.9.3'
}

def getGitCommitHash() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--always', '--dirty=*'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (e) {
        return "unknown"
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version + '-' + getGitCommitHash(), 'mcversion':project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
    dependsOn configurations.runtime
    from {
        configurations.runtime.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    } {
        exclude "dummyThing"
    }

    manifest {
        attributes 'FMLCorePluginContainsFMLMod': 'eu.crushedpixel.replaymod.coremod.LoadingPlugin'
    }
}

defaultTasks 'build'